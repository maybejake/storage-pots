execute if score @s storagepots.total_item_count matches 0 run return run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel is... empty..."}]

#try item name
execute if data entity @s item.components."minecraft:bundle_contents"[0].components."minecraft:item_name" if score @s storagepots.total_item_count matches 1 run return run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{"nbt":"item.components.minecraft:bundle_contents[0].components.minecraft:item_name","entity":"@s","interpret":true},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" item..."}]
execute if data entity @s item.components."minecraft:bundle_contents"[0].components."minecraft:item_name" run return run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{"nbt":"item.components.minecraft:bundle_contents[0].components.minecraft:item_name","entity":"@s","interpret":true},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" items..."}]

#try custom name
execute if data entity @s item.components."minecraft:bundle_contents"[0].components."minecraft:custom_name" if score @s storagepots.total_item_count matches 1 run return run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{"nbt":"item.components.minecraft:bundle_contents[0].components.minecraft:custom_name","entity":"@s","interpret":true},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" item..."}]
execute if data entity @s item.components."minecraft:bundle_contents"[0].components."minecraft:custom_name" run return run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{"nbt":"item.components.minecraft:bundle_contents[0].components.minecraft:custom_name","entity":"@s","interpret":true},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" items..."}]

#default to entity name
summon minecraft:item ~ ~ ~ {Item:{id:"minecraft:cobblestone",count:1},Tags:["storagepots.temp_item","smithed.entity","smithed.strict"]}
data modify entity @n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5] Item.id set from entity @s item.components."minecraft:bundle_contents"[0].id
execute if score @s storagepots.total_item_count matches 1 run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{selector:"@n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5]"},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" item..."}]
execute if score @s storagepots.total_item_count matches 2.. run tellraw @p[tag=storagepots.sherd] [{text:"","color":"gray","italic":true},{"text":"The sherd whispers to you: This vessel contains... "},{selector:"@n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5]"},{"text":"... "},{"score":{"objective":"storagepots.total_item_count","name":"@s"}},{"text":" items..."}]
kill @n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5]