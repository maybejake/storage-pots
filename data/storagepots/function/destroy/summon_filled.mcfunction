$summon minecraft:item ~ ~0.1 ~ {Item:{id:"minecraft:item_frame",count:1,components:{"minecraft:max_stack_size":1,"minecraft:item_name":"Storage Pot","minecraft:item_model":"decorated_pot","minecraft:entity_data":{id:"minecraft:item_frame",Invulnerable:1b,Invisible:1b,Silent:1b,Tags:["storagepots.storage_pot"],Item:$(item)},"minecraft:enchantment_glint_override":true,"minecraft:custom_data":{"smithed":{"ignore":{"functionality":true,"crafting":true}}}}},Motion:[$(strength1),0.15,$(strength2)],Tags:["storagepots.item"]}

#try item name
execute if data entity @s item.components."minecraft:custom_data"."storagepots:contents".components."minecraft:item_name" run return run item modify entity @n[type=minecraft:item,tag=storagepots.item,distance=..0.5] contents {"function":"minecraft:set_lore","entity":"this","lore":[{"nbt":"item.components.minecraft:custom_data.storagepots:contents.components.minecraft:item_name","entity":"@s","interpret":true,color:"white",italic:false,extra:[" x",{"score":{"objective":"storagepots.total_item_count","name":"@s"}}]}],"mode":"replace_all"}

#try custom name
execute if data entity @s item.components."minecraft:custom_data"."storagepots:contents".components."minecraft:custom_name" run return run item modify entity @n[type=minecraft:item,tag=storagepots.item,distance=..0.5] contents {"function":"minecraft:set_lore","entity":"this","lore":[{"nbt":"item.components.minecraft:custom_data.storagepots:contents.components.minecraft:custom_name","entity":"@s","interpret":true,color:"white",italic:false,extra:[" x",{"score":{"objective":"storagepots.total_item_count","name":"@s"}}]}],"mode":"replace_all"}

summon minecraft:item ~ ~ ~ {Item:{id:"minecraft:cobblestone",count:1},Tags:["storagepots.temp_item","smithed.entity","smithed.strict"]}
data modify entity @n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5] Item.id set from entity @s item.components."minecraft:custom_data"."storagepots:contents".id
item modify entity @n[type=minecraft:item,tag=storagepots.item,distance=..0.5] contents {"function":"minecraft:set_lore","entity":"this","lore":[{selector:"@n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5]",color:"white",italic:false,extra:[" x",{"score":{"objective":"storagepots.total_item_count","name":"@s"}}]}],"mode":"replace_all"}
kill @n[type=minecraft:item,tag=storagepots.temp_item,distance=..0.5]